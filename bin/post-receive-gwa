#!/usr/bin/perl
#
#  Copyright (C) 2009 Astaro AG  www.astaro.com
#  All rights reserved.
#
#  Author: Frank Lichtenheld <flichtenheld@astaro.com> 20.07.2009
#
##############################################################################
# General post-receive hook for the various GWA parts
##############################################################################
use strict;
use warnings;

use File::Spec::Functions qw(rel2abs);

use lib '/srv/git/gitwebadmin/lib';
use GitWebAdmin::Schema;

my $schema = GitWebAdmin::Schema->connect("dbi:Pg:dbname=gitwebadmin");
my $rs = $schema->resultset('LogsPush');

my $gitdir = rel2abs($ENV{GIT_DIR});
$gitdir =~ s;^/srv/git/repositories/;;;
$gitdir =~ s;/+$;;;
my $repo = $schema->resultset('Repos')->find($gitdir, {key => 'repos_name_key'})
    or die "Repository $gitdir not found in database\n";
my $gituser = $ENV{GITOSIS_USER};

# Read all data from STDIN
##
while( <> ){
    my ($oldrev, $newrev, $ref) = split /\s+/, $_, 3;

    # Push logging
    ##
    $rs->create({
	rid => $repo->id,
	uid => $gituser,
	old_id => $oldrev,
	new_id => $newrev,
	ref => $ref,
		});
}
