#!/usr/bin/perl
#
#  Copyright (C) 2009 Astaro AG  www.astaro.com
#  All rights reserved.
#
#  Author: Frank Lichtenheld <flichtenheld@astaro.com> 15.07.2009
#
##############################################################################

use strict;
use warnings;

use autodie;
use File::Spec::Functions;
use File::Slurp;
use Git;

use FindBin;
use lib "$FindBin::Bin/../lib";

use Dutil;
use GitWebAdmin::Schema;

my $GIT      = "/srv/git";
my $LOG      = "$GIT/repodata.log";
my $LOCK     = "$GIT/repodata.lock";

Dutil::singularize( $LOCK );
Dutil::daemonize;
Dutil::redirect( $LOG );
Dutil::install_handlers;

Dutil::log("initialized");

my $schema = GitWebAdmin::Schema->connect("dbi:Pg:dbname=gitwebadmin");
my $rs = $schema->resultset('MantisRepos');
my $real_repos = $schema->resultset('Repos');
my $branches = $schema->resultset('Branches');
my $commits = $schema->resultset('Commits');

my $started = time();
while( 1 ){

  my $time = time();
  if( ($time - $started) > (7 * 86_400) ){
    Dutil::log("weekly restart");
    exit 0;
  }

  while( my $repo = $rs->next ){
    my $path = catfile('/srv/git/repositories', $repo->name);
    next unless -d $path;

    my $rrepo = $real_repos->find($repo->id) or die;
    my %branches = map { $_->branch => $_ } $rrepo->branches;

    my $git = Git->repository($path);
    my @heads = $git->command(qw(for-each-ref refs/heads));
    foreach( @heads ){
      unless( m|^([a-f0-9]{40})\s+commit\s+refs/heads/(.+)$| ){
        die "unknown format for for-each-ref output: $_\n";
      }
      my ($sha1, $branch) = ($1, $2);
      if( not exists $branches{$branch} ){
        $rrepo->create_related('branches',
                               { branch => $branch, commit => $sha1 });
      } else {
        $branches{$branch}->commit($sha1);
        $branches{$branch}->update if $branches{$branch}->is_changed;
      }
    }
  }

  sleep 10;
}
