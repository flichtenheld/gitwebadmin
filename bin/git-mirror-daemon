#!/usr/bin/perl
#
#  Copyright (C) 2009,2010 Astaro GmbH & Co. KG  www.astaro.com
#
#  This program is free software; you can redistribute it and/or modify
#  it under the terms of the GNU General Public License as published by
#  the Free Software Foundation; either version 2 of the License, or
#  (at your option) any later version.
#
#  This program is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#
#  You should have received a copy of the GNU General Public License along
#  with this program; if not, write to the Free Software Foundation, Inc.,
#  51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.
#
#  Author: Frank Lichtenheld <flichtenheld@astaro.com> 07.07.2009
#
##############################################################################

use strict;
use warnings;

use autodie;
use File::Spec::Functions;
use File::Slurp;
use Git;
use Config::Auto;

use FindBin;
use Cwd qw(realpath);
use lib "$FindBin::Bin/../lib";

use Dutil;
use GitWebAdmin::Schema;
use GitWebAdmin::Utils;

my $GIT      = "/srv/git";
my $LOG      = "$GIT/mirror.log";
my $LOCK     = "$GIT/mirror.lock";
my $TIMEOUT  = 600; # 10 minutes

Dutil::singularize( $LOCK );
Dutil::daemonize;
Dutil::redirect( $LOG );
Dutil::install_handlers;

Dutil::log("initialized");

my $GWA    = realpath("$FindBin::Bin/../");
my $config = Config::Auto::parse("$GWA/gitwebadmin.ini");

my $schema = GitWebAdmin::Schema->connect("dbi:Pg:dbname=gitwebadmin");
my $rs = $schema->resultset('Repos')->search({ deleted => 0 });

my $lp = $schema->resultset('LogsPush');
# fake data, we don't know what changed
my $upd = $lp->new({ ref => 'refs/head/master',
                     old_id => '0'x40,
                     new_id => '0'x40,
                     uid => 'gitadm' });


my $started = time();
while( 1 ){

  my $time = time();
  if( ($time - $started) > (7 * 86_400 + 50 * 60) ){
    Dutil::log("weekly restart");
    exit 0;
  }

  $rs->reset;
  while( my $repo = $rs->next ){
    next unless $repo->mirrorof;

    my $path = catfile('/srv/git/repositories', $repo->name);
    my $ts_file = catfile( $path, 'mirror_ts' );

    if( -d $path ){
      my $timestamp = '';
      if( -f $ts_file ){
        $timestamp = read_file($ts_file) || '';
        if( $timestamp
            and ($timestamp =~ /^\d+$/)
            and (($time - $timestamp) < $repo->mirrorupd) ){
          next;
        }
      }

      Dutil::log("update mirror $path (ts=$timestamp)");
      my $git = Git->repository($path);
      # update the mirror URI in case it has changed
      $git->command_noisy(qw(config remote.origin.url), $repo->mirrorof);
      eval {
        local $SIG{ALRM} = sub { die "Timeout\n" };
        alarm $TIMEOUT;
        $git->command_noisy(qw(fetch --update-head-ok));
        alarm 0;
      };
      if ($@) {
        die unless $@ eq "Timeout\n";
        Dutil::log("git fetch timed out");
        next;
      }
      write_file($ts_file, time());
      Dutil::log("update branch data");
      GitWebAdmin::Utils::update_branch_data($git, $repo, 'gitadm');
      Dutil::log("update complete");
      next;
    }

    Dutil::log("create mirror $path");
    eval {
      local $SIG{ALRM} = sub { die "Timeout\n" };
      alarm $TIMEOUT;
      system(qw(git clone --mirror), $repo->mirrorof, $path)
        and die "clone failed: $!\n";
      alarm 0;
    };
    if ($@) {
      die unless $@ eq "Timeout\n";
      Dutil::log("git clone timed out");
      next;
    }
    write_file($ts_file, time());
    Dutil::log("run gitosis post-update hook");
    my $gitdir = '/srv/git/repositories/gitosis-admin.git';
    $ENV{GIT_DIR} = $gitdir;
    system(qw(gitosis-run-hook post-update))
      and die "gitosis failed: $!\n";
    delete $ENV{GIT_DIR};
    Dutil::log("update branch data");
    my $git = Git->repository($gitdir);
    GitWebAdmin::Utils::update_branch_data($git, $repo);
    my @triggers = $repo->triggers;
    foreach my $trigger (@triggers){
      Dutil::log($repo->name.": Calling trigger ".$trigger->name);
      GitWebAdmin::Utils::call_trigger($config, $repo, $upd, $trigger);
    }
    Dutil::log("clone complete");
  }

  sleep 30;
}
